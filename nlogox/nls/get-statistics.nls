; ## Procedures to Find Statistics in a Series

; 1. Add `.min`, `.max` as global variables.
; 2. Add `.mean-vector`, and `.mean`.
; 3. Add `.median-vector`, and `.median`.
; 4. Uncomment the procedures (select the code and press `Ctrl+;`).
; 5. Call the procedure at the beginning of `go`.
;    Example: `min-var "tmin"` and `max-var "tmin"`.
; 6. In the *Interface* tab, uncheck *View Updates*.
; 7. Open the *Globals Monitor* (*Tools > Globals Monitor*).
; 8. Run the model until the end of the series.

; Obs: Remove settings after use.

; globals [
;   .min
;   .max
;   .mean
;   .mean-vector
;   .median
;   .median-vector
; ]

to get-min-var [#var]
  let #min min
    [(runresult #var)] of patches with [not is-false? (runresult #var)]

  if (index = 0) [set .min #min]
  if (#min < .min) [set .min #min]
end

to get-max-var [#var]
  let #max max
    [(runresult #var)] of patches with [not is-false? (runresult #var)]

  if (index = 0) [set .max #max]
  if (#max > .max) [set .max #max]
end

to get-mean-var [#var]
  let #mean mean
    [(runresult #var)] of patches with [not is-false? (runresult #var)]

  ifelse (index = 0) [
    set .mean-vector (list #mean)
  ] [
    set .mean-vector lput #mean .mean-vector
  ]

  set .mean mean .mean-vector
end

to get-median-var [#var]
  let #median median
    [(runresult #var)] of patches with [not is-false? (runresult #var)]

  ifelse (index = 0) [
    set .median-vector (list #median)
  ] [
    set .median-vector lput #median .median-vector
  ]

  set .median median .median-vector
end
