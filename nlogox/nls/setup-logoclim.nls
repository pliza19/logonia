; extensions [ls]

; globals [
;   tmax-ls-model
;   tmin-ls-model
;   prec-ls-model
; ]

to setup-logoclim
  let #logoclim-path "../logoclim/nlogox/logoclim.nlogox"
  let #data-path "../../data/" ; Relative to LogoClim `.nlogox` file.
  let #data-series "Historical Monthly Weather Data"
  let #data-resolution "10 Minutes (~340 km2 at the Equator)"

  ls:create-models 3 #logoclim-path

  set tmin-ls-model 0
  set tmax-ls-model 1
  set prec-ls-model 2

  ls:let $data-path #data-path
  ls:let $data-series #data-series
  ls:let $data-resolution #data-resolution
  ls:let $month start-month
  ls:let $year start-year

  ls:ask ls:models [
    set data-path $data-path
    set data-series $data-series
    set data-resolution $data-resolution
    set start-month $month
    set start-year $year
  ]

  ls:ask tmin-ls-model [
    set climate-variable "Average Minimum Temperature (°C)"
  ]

  ls:ask tmax-ls-model [
    set climate-variable "Average Maximum Temperature (°C)"
  ]

  ls:ask prec-ls-model [
    set climate-variable "Total Precipitation (mm)"
  ]

  ls:ask ls:models [setup true]
end
